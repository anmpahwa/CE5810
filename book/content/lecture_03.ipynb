{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "403d1152",
   "metadata": {},
   "source": [
    "# Lecture 03: Least Cost Path Problem\n",
    "\n",
    "```{note}\n",
    "This lecture introduces the Least Cost Path Problem, its mathematical formulation, and Dijkstra's algorithm for finding the shortest path in a network. Both manual and computational approaches are discussed, with illustrative examples and step-by-step procedures.\n",
    "```\n",
    "\n",
    "---\n",
    "\n",
    "## Defintion\n",
    "\n",
    "For a network modeled as a directed graph $G=(N,A)$, with $N$ and $A$ representing the set of nodes and arcs, respectively, the objective of a Least-Cost Problem is to find the path from an origin node $r$ to a destination node $s$ that renders least possible cost, given that each arc $(i,j) \\in A$ has a cost $c_{ij}$.\n",
    "\n",
    "<p align=\"center\">\n",
    "  <img src=\"https://raw.githubusercontent.com/anmpahwa/CE5810/refs/heads/main/resources/LeastCostPath.png\" />\n",
    "</p>\n",
    "<p align=\"center\">\n",
    "  <b>Figure 1.</b> Transportation Network\n",
    "</p>\n",
    "\n",
    "Objective:\n",
    "\n",
    "$$\n",
    "\\min_{\\mathbf{x}} z = \\sum_{(i,j)\\in A} c_{ij}x_{ij}\n",
    "$$\n",
    "\n",
    "Subject to:\n",
    "\n",
    "$$\n",
    "\\begin{aligned}\n",
    "\\sum_{k \\in H(r)} x_{rk} & = 1 \\\\\n",
    "\\sum_{k \\in T(s)} x_{ks} & = 1 \\\\\n",
    "\\sum_{i \\in T(j)} x_{ij} & = \\sum_{k \\in H(j)} x_{jk} & \\ \\forall \\ j \\in N \\\\\n",
    "x_{ij} & \\in \\{0,1\\} & \\ \\forall \\ (i,j) \\in A \\\\\n",
    "\\end{aligned}\n",
    "$$\n",
    "\n",
    "Here, $T(i)$ represents the set of predecessor (tail) nodes of node $i$, while $H(i)$ represents the set of successor (head) nodes, defined as follows,\n",
    "\n",
    "$$\n",
    "\\begin{aligned}\n",
    "T(i) & = \\{k; \\ (k,i) \\in A\\} \\\\\n",
    "H(i) & = \\{k; \\ (i,k) \\in A\\}\n",
    "\\end{aligned}\n",
    "$$\n",
    "\n",
    "Note, $x_{ij}$ represents traversal on arc $(i,j)$.\n",
    "\n",
    "---\n",
    "\n",
    "## Dijsktra's Algorithm\n",
    "\n",
    "### Labeling Algorithm\n",
    "\n",
    "1. **Procedure** $\\text{label}(r, G=(N, A))$\n",
    "2. $X ← \\{n; n \\in N\\}$ &emsp;<small>// initialize a set of open nodes</small>\n",
    "3. $L ← \\{0; n \\in N\\}$ &emsp;<small>// initialize predecessor label for each node </small>\n",
    "4. $C ← \\{\\infty; n \\in N\\}$ &emsp;<small>// initialize cost label for each node</small>\n",
    "5. $i ← r$ &emsp;<small>// set pivot node to origin node</small>\n",
    "6. $P_i ← r$ &emsp;<small>// update predecessor label</small>\n",
    "7. $C_i ← 0$ &emsp;<small>// update cost label</small>\n",
    "8. $X ← X \\backslash \\{i\\}$ &emsp;<small>// remove the pivot node from the set of open nodes</small>\n",
    "9. **while** $X \\ne \\phi $ **do** &emsp;<small>// iterate until all nodes have been visited</small>\n",
    "10. &emsp; **for** $j \\in H(i)$ **do** &emsp;<small>// loop over all successor nodes of the pivot nodes</small>\n",
    "11. &emsp;&emsp; $c_i ← C[i]$\n",
    "12. &emsp;&emsp; $c_j ← C[j]$\n",
    "13. &emsp;&emsp; $c_{ij} ← c_a(A_{ij})$ &emsp;<small>// evaluate traversal cost from the pivot node to the head node</small>\n",
    "14. &emsp;&emsp; **if** $c_i + c_{ij} < c_j$ **then** &emsp;<small>// compare costs</small>\n",
    "15. &emsp;&emsp;&emsp; $L_j ← i$ &emsp;<small>// update predecessor label</small>\n",
    "16. &emsp;&emsp;&emsp; $C_j ← c_i + c_{ij}$ &emsp;<small>// update cost label</small>\n",
    "17. &emsp;&emsp; **end if**\n",
    "18. &emsp; **end for**\n",
    "19. &emsp; $i ← \\text{argmin}\\{C_k; k \\in X\\}$ &emsp;<small>// set pivot node to an open node with least cost label</small>\n",
    "20. &emsp; $X ← X \\backslash \\{i\\}$ &emsp;<small>// remove the pivot node from the set of open nodes</small>\n",
    "21. **end while**\n",
    "22. **return** $L$ &emsp;<small>// return predecessor labels</small>\n",
    "\n",
    "### Path Algorithm\n",
    "\n",
    "1. **Procedure** $\\text{path}(r, s, L)$\n",
    "2. $P ← \\phi$ &emsp;<small>// initialize path vector</small>\n",
    "3. $t ← s$ &emsp;<small>// set tail node to destination</small>\n",
    "4. $h ← s$ &emsp;<small>// set head node to destination</small>\n",
    "5. **while** $h \\ne r$ **do**\n",
    "6. &emsp; $t ← L_h$ &emsp;<small>// set tail node to the predecessor of head node</small>\n",
    "7. &emsp; $P ← P \\cup \\{(t,h)\\}$ &emsp;<small>// add arc to path</small>\n",
    "8. &emsp; $h ← t$&emsp;<small>// add pivot node to path</small>\n",
    "9. **end while**\n",
    "10. $P ← \\text{reverse}(P)$ &emsp;<small>// reverse path vector</small>\n",
    "11. **return** $P$ &emsp;<small>// return path vector</small>\n",
    "\n",
    "---\n",
    "\n",
    "## Implementation\n",
    "\n",
    "### Manual\n",
    "\n",
    "<p align=\"center\">\n",
    "  <img src=\"https://raw.githubusercontent.com/anmpahwa/CE5810/refs/heads/main/resources/ElDoradoNetwork.png\" />\n",
    "</p>\n",
    "<p align=\"center\">\n",
    "  <b>Figure 2.</b> El Dorado City Network\n",
    "</p>\n",
    "\n",
    "#### Initialize\n",
    "\n",
    "| Node | P | C        | Open  |\n",
    "|------|---|----------|-------|\n",
    "| 1    | 0 | $\\infty$ | True  |\n",
    "| 2    | 0 | $\\infty$ | True  |\n",
    "| 3    | 0 | $\\infty$ | True  |\n",
    "| 4    | 0 | $\\infty$ | True  |\n",
    "| 5    | 0 | $\\infty$ | True  |\n",
    "| 6    | 0 | $\\infty$ | True  |\n",
    "\n",
    "#### Iteration #1\n",
    "\n",
    "Pivot: 1\n",
    "\n",
    "Table:\n",
    "\n",
    "| Node | P | C        | Open  |\n",
    "|------|---|----------|-------|\n",
    "| 1    | 1 | 0        | False |\n",
    "| 2    | 1 | 20       | True  |\n",
    "| 3    | 1 | 40       | True  |\n",
    "| 4    | 0 | $\\infty$ | True  |\n",
    "| 5    | 0 | $\\infty$ | True  |\n",
    "| 6    | 0 | $\\infty$ | True  |\n",
    "\n",
    "\n",
    "#### Iteration #2\n",
    "\n",
    "Pivot: 2\n",
    "\n",
    "Table:\n",
    "\n",
    "| Node | P | C        | Open  |\n",
    "|------|---|----------|-------|\n",
    "| 1    | 1 | 0        | False |\n",
    "| 2    | 1 | 20       | False |\n",
    "| 3    | 2 | 30       | True  |\n",
    "| 4    | 2 | 70       | True  |\n",
    "| 5    | 2 | 90       | True  |\n",
    "| 6    | 0 | $\\infty$ | True  |\n",
    "\n",
    "#### Iteration #3\n",
    "\n",
    "Pivot: 3\n",
    "\n",
    "Table:\n",
    "\n",
    "| Node | P | C        | Open  |\n",
    "|------|---|----------|-------|\n",
    "| 1    | 1 | 0        | False |\n",
    "| 2    | 1 | 20       | False |\n",
    "| 3    | 2 | 30       | False |\n",
    "| 4    | 2 | 70       | True  |\n",
    "| 5    | 3 | 80       | True  |\n",
    "| 6    | 0 | $\\infty$ | True  |\n",
    "\n",
    "#### Iteration #4\n",
    "\n",
    "Pivot: 4\n",
    "\n",
    "Table:\n",
    "\n",
    "| Node | P | C        | Open  |\n",
    "|------|---|----------|-------|\n",
    "| 1    | 1 | 0        | False |\n",
    "| 2    | 1 | 20       | False |\n",
    "| 3    | 2 | 30       | False |\n",
    "| 4    | 2 | 70       | False |\n",
    "| 5    | 3 | 80       | True  |\n",
    "| 6    | 4 | 150      | True  |\n",
    "\n",
    "#### Iteration #5\n",
    "\n",
    "Pivot: 5\n",
    "\n",
    "Table:\n",
    "\n",
    "| Node | P | C        | Open  |\n",
    "|------|---|----------|-------|\n",
    "| 1    | 1 | 0        | False |\n",
    "| 2    | 1 | 20       | False |\n",
    "| 3    | 2 | 30       | False |\n",
    "| 4    | 2 | 70       | False |\n",
    "| 5    | 3 | 80       | False |\n",
    "| 6    | 5 | 100      | True  |\n",
    "\n",
    "#### Iteration #6\n",
    "\n",
    "Pivot: 6\n",
    "\n",
    "Table:\n",
    "\n",
    "| Node | P | C   | Open  |\n",
    "|------|---|-----|-------|\n",
    "| 1    | 1 | 0   | False |\n",
    "| 2    | 1 | 20  | False |\n",
    "| 3    | 2 | 30  | False |\n",
    "| 4    | 2 | 70  | False |\n",
    "| 5    | 3 | 80  | False |\n",
    "| 6    | 5 | 100 | False |\n",
    "\n",
    "The path from node 6 can be traced backwards using predecessor table: $6 \\leftarrow 5 \\leftarrow 3 \\leftarrow 2 \\leftarrow 1$\n",
    "\n",
    "### Computational"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "de7c54c0",
   "metadata": {},
   "outputs": [],
   "source": [
    "struct Node\n",
    "  i::Int64\n",
    "  T::Vector{Int64}\n",
    "  H::Vector{Int64}\n",
    "end\n",
    "H(n) = n.H\n",
    "T(n) = n.T\n",
    "\n",
    "struct Arc\n",
    "  t::Int64\n",
    "  h::Int64\n",
    "  c::Float64\n",
    "end\n",
    "\n",
    "struct Graph\n",
    "  N::Vector{Node}\n",
    "  A::Matrix{Arc}\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d54f1c2d",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "path (generic function with 3 methods)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "# Labeling Algorithm\n",
    "function label(r::Node, G::Graph)\n",
    "  N = G.N\n",
    "  A = G.A\n",
    "  X = [n.i for n ∈ N]\n",
    "  L = [Node(0, [], []) for n ∈ N]\n",
    "  C = [Inf for n ∈ N]\n",
    "  i = r.i\n",
    "  L[i] = r\n",
    "  C[i] = 0.\n",
    "  deleteat!(X, i)\n",
    "  while !isempty(X)\n",
    "    for j ∈ H(N[i])\n",
    "      cᵢ  = C[i]\n",
    "      cⱼ  = C[j]\n",
    "      cᵢⱼ = A[i,j].c\n",
    "      if cᵢ + cᵢⱼ < cⱼ\n",
    "        L[j] = N[i]\n",
    "        C[j] = cᵢ + cᵢⱼ\n",
    "      end\n",
    "    end\n",
    "    k = argmin([C[k] for k ∈ X]) \n",
    "    i = X[k]\n",
    "    deleteat!(X, k)\n",
    "  end\n",
    "  return L\n",
    "end\n",
    "\n",
    "# Path Finding Algorithm\n",
    "function path(r::Node, s::Node, L::Vector{Node})\n",
    "  P = Tuple{Int,Int}[]\n",
    "  t = s\n",
    "  h = s\n",
    "  while h != r\n",
    "    t = L[h.i]\n",
    "    push!(P, (t.i,h.i))\n",
    "    h = t\n",
    "  end\n",
    "  reverse!(P)\n",
    "  return P\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 61,
   "id": "f7d3a3bd",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Graph(Node[Node(1, Int64[], [2, 3]), Node(2, [1], [3, 4, 5]), Node(3, [1, 2], [4, 5]), Node(4, [2, 3], [5, 6]), Node(5, [2, 3, 4], [6]), Node(6, [4, 5], Int64[])], Arc[Arc(1, 1, 0.0) Arc(1, 2, 20.0) … Arc(1, 5, Inf) Arc(1, 6, Inf); Arc(2, 1, Inf) Arc(2, 2, 0.0) … Arc(2, 5, 70.0) Arc(2, 6, Inf); … ; Arc(5, 1, Inf) Arc(5, 2, Inf) … Arc(5, 5, 0.0) Arc(5, 6, 20.0); Arc(6, 1, Inf) Arc(6, 2, Inf) … Arc(6, 5, Inf) Arc(6, 6, 0.0)])"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "N = [Node(1, [], [2,3]), Node(2, [1], [3,4,5]), Node(3, [1,2], [4,5]), Node(4, [2,3], [5,6]), Node(5, [2,3,4], [6]), Node(6, [4,5], [])]\n",
    "A = [Arc(1, 1, 0)   Arc(1, 2, 20)  Arc(1, 3, 40)  Arc(1, 4, Inf) Arc(1, 5, Inf) Arc(1, 6, Inf)\n",
    "     Arc(2, 1, Inf) Arc(2, 2, 0)   Arc(2, 3, 10)  Arc(2, 4, 50)  Arc(2, 5, 70)  Arc(2, 6, Inf)\n",
    "     Arc(3, 1, Inf) Arc(3, 2, Inf) Arc(3, 3, Inf) Arc(3, 4, 80)  Arc(3, 5, 50)  Arc(3, 6, Inf)\n",
    "     Arc(4, 1, Inf) Arc(4, 2, Inf) Arc(4, 3, Inf) Arc(4, 4, 0)   Arc(4, 5, 20)  Arc(4, 6, 80)\n",
    "     Arc(5, 1, Inf) Arc(5, 2, Inf) Arc(5, 3, Inf) Arc(5, 4, Inf) Arc(5, 5, 0)   Arc(5, 6, 20)\n",
    "     Arc(6, 1, Inf) Arc(6, 2, Inf) Arc(6, 3, Inf) Arc(6, 4, Inf) Arc(6, 5, Inf) Arc(6, 6, 0)]\n",
    "G = Graph(N, A)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 62,
   "id": "17120aa9",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "4-element Vector{Tuple{Int64, Int64}}:\n",
       " (1, 2)\n",
       " (2, 3)\n",
       " (3, 5)\n",
       " (5, 6)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "r = N[1]\n",
    "s = N[6]\n",
    "\n",
    "L = label(r, G)\n",
    "P = path(r, s, L)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "348fc37d",
   "metadata": {},
   "source": [
    "\n",
    "---\n",
    "\n",
    "```{note}\n",
    "This lecture sets the stage for understanding how traffic is assigned to a network, a fundamental concept in transportation planning. In the next lecture, we will deep dive into traffic assignment methodology, exploring how demand is distributed across available routes, and how user behavior and network characteristics influence route choice and overall system performance.\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.1",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
